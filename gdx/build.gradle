/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

apply plugin: "java"

sourceCompatibility = 1.7
targetCompatibility = 1.7

sourceSets.main.java.srcDirs = ["src"]
sourceSets.main.resources.srcDirs = ["res"]
sourceSets.test.java.srcDirs = ["test"]

compileJava {
	options.fork = true
	options.incremental = true
}

dependencies {
	testCompile libraries.junit
	compile "com.badlogicgames.gdx:gdx-jnigen-loader:2.2.0"
}

test {
	testLogging {
		events "passed", "skipped", "failed", "standardOut", "standardError"
	}
}

// Workaround needed for IDEA to have resources on classpath when running tests (like gdx-tests-lwjgl3)
task copyIdeaResources(type: Copy) {
	from "${projectDir}/res"
	into "${buildDir}/classes/java/main/"
}
processResources.dependsOn copyIdeaResources

apply plugin: "com.badlogicgames.gdx.gdx-jnigen"
jnigen {
	sharedLibName = "gdx"
	temporaryDir = "../target/native"
	all {
		cppExcludes = ["iosgl/**"]
	}
	add(Windows, x32)
	add(Windows, x64)
	add(Linux, x64)
	add(Linux, x32, ARM)
	add(Linux, x64, ARM)
	add(Android) {
		linkerFlags += " -llog"
	}
	add(MacOsX, x64)
	add(MacOsX, x64, ARM)
	add(IOS) {
		headerDirs = ["iosgl"]
		cppExcludes = []
	}
}
