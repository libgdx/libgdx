/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

apply plugin: "java"

if(JavaVersion.current().isJava9Compatible()) {
	compileJava {
		options.release = versions.java
	}
} else {
	sourceCompatibility = versions.java
	targetCompatibility = versions.java
}

sourceSets.main.java.srcDirs = ["src"]
sourceSets.main.resources.srcDirs = ["res"]
sourceSets.test.java.srcDirs = ["test"]

compileJava {
	options.fork = true
	options.incremental = true
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
	testImplementation libraries.junit
	api "com.badlogicgames.gdx:gdx-jnigen-loader:2.3.1"
}

test {
	testLogging {
		events "passed", "skipped", "failed", "standardOut", "standardError"
	}
}

processResources.duplicatesStrategy = DuplicatesStrategy.EXCLUDE

apply plugin: "com.badlogicgames.gdx.gdx-jnigen"
jnigen {
	sharedLibName = "gdx"
	temporaryDir = "../target/native"
	all {
		cppExcludes = ["iosgl/**"]
	}
	add(Windows, x32)
	add(Windows, x64)
	add(Linux, x64)
	add(Linux, x32, ARM)
	add(Linux, x64, ARM)
	add(Android) {
		linkerFlags += " -llog"
	}
	add(MacOsX, x64)
	add(MacOsX, x64, ARM)
	add(IOS) {
		headerDirs = ["iosgl"]
		cppExcludes = []
	}
	robovm {
		if (file("libs/ios32/ObjectAL.xcframework/ios-arm64_armv7/").exists()) {
			extraLib("libs/ObjectAL.xcframework/ios-arm64_armv7/libObjectAL.a", "device")
		} else if (file("libs/ios32/ObjectAL.xcframework/ios-arm64/").exists()) {
			extraLib("libs/ObjectAL.xcframework/ios-arm64/libObjectAL.a", "device")
		} else {
			getLogger().warn("ObjectAL is not build yet. Please run `fetchNatives` or build it to silence this warning.")
		}
		extraLib("libs/ObjectAL.xcframework/ios-arm64_x86_64-simulator/libObjectAL.a", "simulator")
	}
}
