/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

apply from: "gradle/dependencies.gradle"

buildscript {
    apply from: "gradle/dependencies.gradle"

    repositories {
        mavenCentral()
        maven { url("https://oss.sonatype.org/content/repositories/snapshots") }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:${versions.spotless}"
        classpath "com.badlogicgames.gdx:gdx-jnigen-gradle:2.2.2-SNAPSHOT"
    }
}

ext {
    isReleaseBuild = {
        return project.hasProperty("RELEASE")
    }

    getReleaseRepositoryUrl = {
        return project.hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
                : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    }

    getSnapshotRepositoryUrl = {
        return project.hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
                : "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    getRepositoryUsername = {
        return project.hasProperty('MAVEN_USERNAME') ? MAVEN_USERNAME : ""
    }

    getRepositoryPassword = {
        return project.hasProperty('MAVEN_PASSWORD') ? MAVEN_PASSWORD : ""
    }
}

allprojects {
    group = 'com.badlogicgames.gdx'
    version = project.getProperty('version') + (isReleaseBuild() ? "" : "-SNAPSHOT")

    buildscript {
        repositories {
            google()
            mavenLocal()
            mavenCentral()
            gradlePluginPortal()
            maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
            maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        }
    }

    repositories {
        google()
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Test) {
        systemProperty 'file.encoding', 'UTF-8'
    }

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none,-missing', '-quiet')
        }
    }   

    tasks.register('uberJar', Jar) {
        archiveClassifier = ''
        archiveVersion = ''
        duplicatesStrategy = 'warn'


        from sourceSets.main.output

        dependsOn configurations.runtimeClasspath
        from {
            configurations.runtimeClasspath.findAll { it.name.endsWith('jar') && !it.name.endsWith("gdx-" + project.version + ".jar") && !it.name.contains("platform") }.collect { println it; zipTree(it) }
        }
    } 
}

configure(subprojects - project(":tests:gdx-tests-android")) {
    apply plugin: "java-library"
    apply plugin: "com.diffplug.spotless"

    compileJava {
        options.fork = true
        options.incremental = true
    }
    
    java {
        withJavadocJar()
        withSourcesJar()
    }

    spotless {
        lineEndings 'UNIX'

        java {
            removeUnusedImports()
            eclipse().configFile new File(rootProject.projectDir.absolutePath, 'eclipse-formatter.xml')
        }
        groovyGradle {
            target '*.gradle'
            greclipse().configFile new File(rootProject.projectDir.absolutePath, 'eclipse-formatter.xml')
        }
    }
}

configure(subprojects - project(":backends") - project(":extensions") - project(":tests") - project(":extensions:gdx-box2d-parent")) {
    apply plugin: "idea"
    apply plugin: "eclipse"

    //This has to be done in afterEvaluate due to java/android plugin dependency
    afterEvaluate {
        eclipse {
            project {
                //hide gradle build folders from eclipse
                resourceFilter {
                    appliesTo = 'FOLDERS'
                    type = 'EXCLUDE_ALL'
                    matcher {
                        id = 'org.eclipse.ui.ide.multiFilter'
                        arguments = '1.0-projectRelativePath-matches-true-false-build'
                    }
                }
            }
            jdt {
                file {
                    withProperties { properties ->
                        def libgdxJdtProperties = new Properties()
                        libgdxJdtProperties.load(new FileInputStream(".settings/org.eclipse.jdt.libgdx.prefs"))
                        properties.putAll(libgdxJdtProperties)
                    }
                }
            }
            classpath.file {
                whenMerged { classpath ->
                    classpath.entries.findAll { entry -> entry.kind == 'lib' || entry.kind == 'src' }*.exported = true
                }
            }
        }
        //Set all projects to use UTF-8 file encoding for all files
        eclipseJdt.doLast {
            File f = new File(projectDir, '.settings/org.eclipse.core.resources.prefs')
            f.write('eclipse.preferences.version=1\n')
            f.append('encoding/<project>=UTF-8')
        }
    }
}

task fetchNativesZip() {
	doLast {
		def zip = file("build/natives.zip");
		new URL('https://libgdx-nightlies.s3.eu-central-1.amazonaws.com/libgdx-nightlies/natives.zip').withInputStream{ i -> zip.withOutputStream{ it << i }}
	}
	outputs.file(file("build/natives.zip"))
}

task fetchNatives(dependsOn: fetchNativesZip, type: Copy) {
    from zipTree("build/natives.zip")
    into "."
}

apply from: rootProject.file('publish.gradle')

if(rootProject.file('override.gradle').exists())
	apply from: rootProject.file('override.gradle')