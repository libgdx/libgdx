/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

buildscript {
	dependencies {
		classpath "org.wisepersist:gwt-gradle-plugin:${versions.gwtPlugin}"
		classpath "org.gretty:gretty:${versions.gretty}"
	}
}

configurations.all {
	// gwt-dev pulls in apache-jsp (Tomcat), but we don't need it and it messes with gretty
	exclude group: 'org.eclipse.jetty', module: 'apache-jsp'
}

apply plugin: 'gwt'
apply plugin: 'org.gretty'

import org.wisepersist.gradle.plugins.gwt.GwtSuperDev
import org.akhikhl.gretty.AppBeforeIntegrationTestTask

gretty.httpPort = 8080
gretty.resourceBase = project.buildDir.path + "/gwt/draftOut"
gretty.contextPath = "/"
gretty.portPropertiesFileName = "TEMP_PORTS.properties"

dependencies {
	implementation project(":tests:gdx-tests")
	implementation project(":backends:gdx-backend-gwt")
	implementation project(":extensions:gdx-box2d-parent:gdx-box2d-gwt")
}

gwt {
	gwtVersion='2.8.2' // Should match the gwt version used for building the gwt backend
	maxHeapSize="1G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
	minHeapSize="1G"

	src = files(file("src/")) // Needs to be in front of "modules" below.
	modules 'com.badlogic.gdx.tests.gwt.GdxTestsGwt'
	devModules 'com.badlogic.gdx.tests.gwt.GdxTestsGwt'

	compiler.strict = true
	compiler.disableCastChecking = true
}

task startHttpServer () {
	dependsOn draftCompileGwt

	doFirst {
		copy {
			from "webapp"
			into gretty.resourceBase
		}

		copy {
			from "war"
			into gretty.resourceBase
		}
	}
}

task beforeRun(type: AppBeforeIntegrationTestTask, dependsOn: startHttpServer) {
	// The next line allows ports to be reused instead of
	// needing a process to be manually terminated.
	file("build/TEMP_PORTS.properties").delete()
	// Somewhat of a hack; uses Gretty's support for wrapping a task in
	// a start and then stop of a Jetty server that serves files while
	// also running the SuperDev code server.
	integrationTestTask 'superDev'

	interactive false
}

task superDev (type: GwtSuperDev) {
	dependsOn startHttpServer
	doFirst {
		gwt.modules = gwt.devModules
	}
}

task dist(dependsOn: [clean, compileGwt]) {
	doLast {
		file("build/dist").mkdirs()
		copy {
			from "build/gwt/out"
			into "build/dist"
		}
		copy {
			from "webapp"
			into "build/dist"
		}
		copy {
			from "war"
			into "build/dist"
		}
	}
}


task addSource {
	doLast {
		sourceSets.main.compileClasspath += files(project(':tests:gdx-tests').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':tests:gdx-tests').sourceSets.main.resources.srcDirs)
		sourceSets.main.compileClasspath += files(project(':backends:gdx-backend-gwt').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':backends:gdx-backend-gwt').sourceSets.main.resources.srcDirs)
		sourceSets.main.compileClasspath += files(project(':extensions:gdx-box2d-parent:gdx-box2d').sourceSets.main.resources.srcDirs)
		sourceSets.main.compileClasspath += files(project(':extensions:gdx-box2d-parent:gdx-box2d-gwt').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':extensions:gdx-box2d-parent:gdx-box2d-gwt').sourceSets.main.resources.srcDirs)
		sourceSets.main.compileClasspath += files(project(':gdx').sourceSets.main.allJava.srcDirs)
		sourceSets.main.compileClasspath += files(project(':gdx').sourceSets.main.resources.srcDirs)
		sourceSets.main.compileClasspath += files(sourceSets.main.resources.srcDirs)
	}
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)

eclipse {
	classpath {
		containers += [
			'com.google.gwt.eclipse.core.GWT_CONTAINER',
			'com.gwtplugins.gwt.eclipse.core.GWT_CONTAINER'
		]
	}
}
