/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

buildscript {
    dependencies {
        classpath "org.multi-os-engine:moe-gradle:${versions.moePlugin}"
    }
}

apply plugin: 'moe'

sourceSets.main.java.srcDirs = ["src/main/java"]
sourceSets.main.resources.srcDirs = ["src/main/resources"]

dependencies {
    compile project(":tests:gdx-tests")
    compile project(":backends:gdx-backend-moe")
    natives testnatives.ios
}

task copyNatives << {
    file("xcode/native/ios/").mkdirs();
    def LD_FLAGS = "LIBGDX_NATIVES = "
    configurations.natives.files.each { nativeFolder  ->
        def outputDir = file("xcode/native/ios")
        def nativeFiles = nativeFolder.listFiles()
        nativeFiles.each { nativeFile ->
            copy {
                from nativeFile.getAbsolutePath()
                into outputDir
            }
            LD_FLAGS += " -force_load \${SRCROOT}/native/ios/" + nativeFile.getName()
        }
    }
    def outFlags = file("xcode/IOSTests/custom.xcconfig");
    outFlags.write LD_FLAGS

    def proguard = file("proguard.append.cfg")
    if (!proguard.exists()) {
        proguard = new File("proguard.append.cfg")
        proguard << "-keep class com.badlogic.** { *; }\n"
        proguard << "-keep enum com.badlogic.** { *; }\n"
    }
}

moe {
    xcode {
        mainTarget 'IOSTests'
        packageName 'com.badlogicgames.gdx.tests'
        deploymentTarget = '9.0'
        xcodeProjectDirPath 'xcode'
        generateProject false
    }
}

moeMainReleaseIphoneosXcodeBuild.dependsOn copyNatives
moeMainDebugIphoneosXcodeBuild.dependsOn copyNatives
moeMainReleaseIphonesimulatorXcodeBuild.dependsOn copyNatives
moeMainDebugIphonesimulatorXcodeBuild.dependsOn copyNatives
