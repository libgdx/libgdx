/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

buildscript {
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidPlugin}"
    }
}

apply plugin: "com.android.application"
apply from: "obb.gradle"

dependencies {
    implementation project(":tests:gdx-tests")
    implementation project(":gdx")
    implementation project(":extensions:gdx-box2d:gdx-box2d-core")
    implementation project(":extensions:gdx-bullet")
    implementation project(":extensions:gdx-controllers:gdx-controllers-android")
    implementation project(":extensions:gdx-freetype")
    implementation project(":backends:gdx-backend-android")
    implementation libraries.android
    implementation "androidx.multidex:multidex:${versions.multiDex}"

    // for wear os watch face
    // If use remote binary 'com.google.android.support:wearable:2.7.0', an error will occur while compiling:
    // Program type already present. This issue may be related to AndroidX compatibility, android gradle plugin must be upgraded
    // to 3.2.0+ to solve the problem. But I encountered some problems when upgrading the version, see:
    // https://github.com/libgdx/libgdx/issues/6038
    // Adding local dependencies is a workaround. Because 'libs' folder here is created by fetch.xml and has other uses, so I
    // created a new directory called 'dependencies' to save the dependent jars.
    implementation files('dependencies/wearable-2.7.0.jar')
}

android {
    buildToolsVersion versions.androidBuildTools
    compileSdkVersion versions.androidSdk
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        multiDexEnabled true
    }

    lintOptions {
        // Since this is a test apk, it is fine to ignore the missing densities for the icon.
        disable 'IconMissingDensityFolder'
    }
}

